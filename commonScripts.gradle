/*
 * Copyright (c) 2021. Patryk Goworowski
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply from: "$rootDir/compilerArgs.gradle"

android {
    compileSdkVersion library.target_sdk

    defaultConfig {
        minSdkVersion library.min_sdk
        targetSdkVersion library.target_sdk

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

ext {
    getNamespace = this.&getNamespace
    getArtifactId = this.&getArtifactId
    customizePom = this.&customizePom
}

String getNamespace(Project project) {
    return "${library.groupId}.${project.name.replace("-", ".")}"
}

String getArtifactId(Project project) {
    return "${project.parent.name}-${project.name}"
}

void customizePom(MavenPom pom) {
    pom.licenses {
        license {
            name = "The Apache License, Version 2.0"
            url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
        }
    }
    pom.scm {
        connection = "scm:git:git://github.com/VicoChart/VicoChart.git"
        developerConnection = "scm:git:ssh://github.com/VicoChart/VicoChart.git"
        url = "https://github.com/VicoChart/VicoChart"
    }
    pom.developers {
        developer {
            id = "patrykgoworowski"
            name = "Patryk Goworowski"
            email = "patrykgoworowski@gmail.com"
        }
        developer {
            id = "patrykmichalik"
            name = "Patryk Michalik"
            email = "contact@patrykmichalik.com"
        }
    }
}
